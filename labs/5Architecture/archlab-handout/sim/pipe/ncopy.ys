#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	xorq %rax, %rax
	andq %rdx, %rdx
	jle Done
	iaddq $-2, %rdx
	jl Tail1
	je Tail2

Loop:
	mrmovq (%rdi), %rcx
	mrmovq 8(%rdi), %rbx
	mrmovq 16(%rdi), %rbp
	rmmovq %rcx, (%rsi)
	rmmovq %rbx, 8(%rsi)
	rmmovq %rbp, 16(%rsi)

	andq %rcx, %rcx
	jle compare1
	iaddq $1, %rax

compare1:
	andq %rbx, %rbx
	jle compare2
	iaddq $1, %rax

compare2:
	andq %rbp, %rbp
	jle Npos
	iaddq $1, %rax

Npos:
	iaddq $24, %rdi
	iaddq $24, %rsi
	iaddq $-3, %rdx
	jg Loop
	iaddq $1, %rdx
	je Tail1
	jl Done

Tail2:
	mrmovq (%rdi), %rbx
	rmmovq %rbx, (%rsi)
	iaddq $8, %rdi
	iaddq $8, %rsi
	andq %rbx, %rbx
	jle Tail1
	iaddq $1, %rax

Tail1:
	mrmovq (%rdi), %rcx
	rmmovq %rcx, (%rsi)
	andq %rcx, %rcx
	jle Done
	iaddq $1, %rax


##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
